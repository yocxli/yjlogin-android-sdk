// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.9.10'
    ext.dokka_version = '1.9.10'
    ext.ktlint_version = '0.36.0'
    ext.maven_gradle_plugin_version = '2.1'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.10.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$maven_gradle_plugin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
    // 脆弱性対応
    configurations.configureEach {
        resolutionStrategy {
            force "org.bouncycastle:bcprov-jdk18on:1.78"
            force "com.fasterxml.jackson.core:jackson-core:2.15.0-rc1"
            force "com.google.protobuf:protobuf-java-util:3.25.5"
            force "com.google.protobuf:protobuf-java:3.25.5"
            force "io.netty:netty-codec-http2:4.1.125.Final"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:$ktlint_version"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--android", "--color", "--reporter=plain", "**/src/main/**/*.kt"
    ignoreExitValue true
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "--android", "--color", "--reporter=plain", "**/src/main/**/*.kt"
    ignoreExitValue true
}
